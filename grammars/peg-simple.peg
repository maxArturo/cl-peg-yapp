# PEG simplified
#
# This is the flavor of PEG that `cl-peg-yapp` works with. 
# It's simplified from the more hardcore PEGN for my use cases.
# 
# Note that the following char classes are predefined in the
# parser for convenience: 
#
# numeric
# alpha
# alphanum

Grammar    <- ComEndLine* (Definition ComEndLine*)+


ComEndLine <- ' '* ('# ' Comment)? EndLine
Comment    <- (!EndLine alphanum)+
Spacing    <- ComEndLine? ' '+
EndLine    <-   u000D         # CR
              / u000A         # LF
              / (u000D u000A)

# Definition section
Definition <- CheckId ' '+ '<-' ' '+ Expression 

CheckId    <- ([A-Z] [a-z]+)+

Expression <- Sequence (Spacing* '/' ' '* Sequence)*
Sequence   <- Rule (Spacing Rule)*
Rule       <- PosLook / NegLook / Plain
PosLook    <- '&' Primary Quant?
NegLook    <- '!' Primary Quant?
Plain      <- Primary Quant?

Primary    <- Simple / CheckId / '(' Expression ')'

Simple     <- '[' (!']' ( (alphanum '-' alphanum) / alphanum) )+ ']'

Quant      <- Optional / MinZero / MinOne / MinMax / Amount

Optional   <- '?'
MinZero    <- '*'
MinOne     <- '+'
MinMax     <- '{' numeric ',' numeric '}'
Amount     <- '{' numeric '}'

